group 'ch.bildspur.visualpush'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        maven { url "https://mini2dx.org/maven/content/repositories/thirdparty" }
        maven { url "https://mini2dx.org/maven/content/repositories/releases" }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath group: 'org.mini2Dx', name: 'parcl', version: '1.2.1'
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: "org.mini2Dx.parcl"

mainClassName = "ch.bildspur.visualpush.Main"
applicationDefaultJvmArgs = ["-Djava.library.path=lib/video/macosx64:lib/syphon:lib/themidibus"]

parcl {
    app {
        vmArgs = ["-Djava.library.path=lib/video/macosx64:lib/syphon:lib/themidibus"]
        appName = "VisualPushX"
        icon = "icons/icon.icns"
        applicationCategory = "public.app-category.tools"
        displayName = 'Visual Push X'
        identifier = 'ch.bildspur.visualpushx'
        copyright = 'Copyright 2018 bildspur'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // processing
    compile group: 'org.processing', name: 'core', version: '3.3.6'
    compile group: 'org.jogamp.jogl', name: 'jogl-all-main', version: '2.3.2'
    compile group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version: '2.3.2'

    // post fx
    compile files('lib/postfx/PostFX.jar')

    // midibus
    compile files('lib/themidibus/themidibus.jar')
    compile files('lib/themidibus/mmj.jar')

    // syphon
    compile files('lib/syphon/jsyphon.jar')
    compile files('lib/syphon/Syphon.jar')

    // video
    compile files('lib/video/gst1-java-core-0.9.1.jar')
    compile files('lib/video/jna.jar')
    compile files('lib/video/video.jar')

    // native
    compile group: 'org.usb4java', name: 'usb4java', version: '1.2.0'
    compile group: 'org.usb4java', name: 'usb4java-javax', version: '1.2.0'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}